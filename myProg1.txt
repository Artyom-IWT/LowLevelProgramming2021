





























T 95 S
Z 0 S [ останов для отладки ]
T 0 S [ запись аккумулятора в рабочую ячейку 0, обнуление аккумулятора ]
A 82 [<len>] S [ загрузка в аккумулятор длины массива ]
U 1 S [ запись длины массива в ячейку 1 ]
A 83 [<addr>] S [ загрузка в аккумулятор адреса 0-го элемента массива ]
L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
S 81 [<c2>] S [ вычитаем 2 для получения адреса последнего элемента ]
A 61 [<r2>] S [ прибавляем код инструкции с нулевым полем адреса ]
T 61 [<r2>] S [ запись сформированной инструкции, обнуление аккумулятора ]
A 82 [<len>] S [ загрузка в аккумулятор длины массива ]
A 83 [<addr>] S [ загрузка в аккумулятор адреса 0-го элемента массива ]
L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
S 81 [<c2>] S [ вычитаем 2 для получения адреса последнего элемента ]
A 64 [<w2>] S [ прибавляем код инструкции с нулевым полем адреса ]
T 64 [<w2>] S [ запись сформированной инструкции, обнуление аккумулятора ]
A 83 [<addr>] S [ загрузка в аккумулятор адреса 0-го элемента массива ]
L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
A 59 [<r1>] S [ прибавляем код инструкции с нулевым полем адреса ]
T 59 [<r1>] S [ запись сформированной инструкции, обнуление аккумулятора ]
A 83 [<addr>] S [ загрузка в аккумулятор адреса 0-го элемента массива ]
L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
A 62 [<w1>] S [ прибавляем код инструкции с нулевым полем адреса ]
T 62 [<w1>] S [ запись сформированной инструкции, обнуление аккумулятора ]
[loop:] A 1 S [ загружаем счетчик необработанных элементов массива ]
S 81 [<c2>] S [ уменьшаем на 2 ]
G 80 [<exit>] S [ если результат меньше 0, завершаем работу ]
T 1 S [ обновляем значение счетчика и обнуляем аккумулятор ]
[r1:] A 0 S [ загрузка в аккумулятор значения из ячейки N ]
T 0 S [ запись в рабочую ячейку 0, обнуление аккумулятора ]
[r2:] A 0 S [ загрузка в аккумулятор значения из зеркальной ячейки ]
[w1:] T 0 S [ запись в ячейку с адресом N, обнуление аккумулятора ]
A 0 S [ загрузка в аккумулятор значения из ячейки 0 ]
[w2:] T 0 S [ запись в зеркальную ячейку, обнуление аккумулятора ]
A 1 [<1>] S [ загрузка в аккумулятор значения из ячейки 1 - счётчика ]
L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
A 59 [<r1>] S [ прибавляем код инструкции с полем адреса элемента N]
T 61 [<r2>] S [ запись сформированной инструкции, обнуление аккумулятора ]
A 1 [<1>] S [ загрузка в аккумулятор значения из ячейки 1 - счётчика ]
L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
A 62 [<w1>] S [ прибавляем код инструкции с полем адреса элемента N]
T 64 [<w2>] S [ запись сформированной инструкции, обнуление аккумулятора ]
A 81 [<c2>] S [ загрузка в аккумулятор константы 2 - вместо 1 и сдвига ]
A 59 [<r1>] S [ прибавляем код инструкции с полем адреса элемента N]
T 59 [<r1>] S [ запись сформированной инструкции, обнуление аккумулятора ]
A 81 [<c2>] S [ загрузка в аккумулятор константы 2 - вместо 1 и сдвига  ]
A 62 [<w1>] S [ прибавляем код инструкции с полем адреса элемента N]
T 62 [<w1>] S [ запись сформированной инструкции, обнуление аккумулятора ]
E 55 [<loop>] S [ повторяем все операции ]
[exit:]Z 0 S [ останов ]
[с2:] P 1 S [ константа 2 ]
[len:] P 5 L [ длина массива = 11]
[addr:] P 42 S [ 84 - адрес 0-го элемента массива ]
[array:] P 0 S [ 0 ]
P 0 L [ 1 ]
P 1 S [ 2 ]
P 1 L [ 3 ]
P 2 S [ 4 ]
P 2 L [ 5 ]
P 3 S [ 6 ]
P 3 L [ 7 ]
P 4 S [ 8 ]
P 4 L [ 9 ]
P 5 S [ 10 ]

